buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.1.1"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: "com.jfrog.artifactory"

// This is the version used as the 'versionName' for the aar library, and also used when deploying to Artifactory
def versionLabel = "2.5.1"

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 23
        versionCode 1
        versionName versionLabel
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:support-v4:23.1.0'
}

artifactory {
    contextUrl = "http://bamboo.vidmark.local:8081/artifactory"
    publish {
        repository {
            repoKey = "libs-release-local"
            username = "admin"
            password = "password"
        }
        defaults {
          publishArtifacts = true
          publications ('ViewPagerIndicator')
          publishPom = true
          publishIvy = false
        }
    }
}

publishing {
    publications {
        ViewPagerIndicator(MavenPublication) {
            groupId 'com.viewpagerindicator'
            artifactId 'ViewPagerIndicator'
            version versionLabel
            artifact("$buildDir/outputs/aar/library-release.aar")
        }
    }
}

artifactoryPublish { 
    dependsOn assemble
}
